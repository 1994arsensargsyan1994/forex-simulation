buildscript {
    ext {
        springBootVersion = '3.0.5'
        springCloudVersion = '2022.0.1'
        testcontainersVersion = '1.17.6'
    }
}

plugins {
    id 'com.google.cloud.tools.jib' version '3.2.0' apply false

    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version '1.1.0'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'java-library'

    apply plugin: 'io.spring.dependency-management'

    group = 'org.arsen'
    version = '1.0-SNAPSHOT'

    sourceCompatibility = JavaVersion.VERSION_17

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"

            mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
        }
        dependencies {
            dependency "org.springdoc:springdoc-openapi-ui:1.6.14"
            dependency "org.testcontainers:postgresql:${testcontainersVersion}"
        }
    }

    dependencies {
        implementation 'org.apache.commons:commons-lang3'
        implementation 'org.slf4j:jul-to-slf4j'

        testImplementation 'org.mockito:mockito-core'
        testImplementation 'org.testcontainers:junit-jupiter'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()

        testLogging {
            events 'passed', 'skipped', 'failed'
        }
    }

    jacoco {
        toolVersion = '0.8.10'
    }

    jacocoTestReport {
        reports {
            xml.required = true
        }
    }
}